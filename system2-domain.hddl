(define (domain system2)
    (:requirements :hierarchy :typing)
    (:types 
        webapp_object
        jsondb_object
        task_struct
        task_object
        system2_api
        system2_app
    )
    (:predicates
        (web_app_object_created ?wo - webapp_object)
        (json_db_object_created ?jo - jsondb_object)
        (api_developed ?api - system2_api)
        (api_get_tasks_developed ?api - system2_api)
        (app_developed ?app - system2_app)
        (task_data_struct_developed ?ts - task_struct)
        (api_create_task_developed ?api - system2_api)
        (api_decompose_task_developed ?api - system2_api)
    )
    
    ; tasks
    (:task t_develop_app
        :parameters (?app - system2_app)
    )
    (:task t_develop_api
        :parameters (?api - system2_api)
    )
    (:task t_develop_api_get_tasks
        :parameters (?api - system2_api)
    )
    (:task t_develop_api_create_task
        :parameters (?api - system2_api)
    )
    (:task t_develop_api_decompose_task
        :parameters (?api - system2_api)
    )

    ; methods
    (:method m_develop_app
        :parameters (?app - system2_app ?api - system2_api)
        :task (t_develop_app ?app)
        :precondition (and
            (not(app_developed ?app))
        )
        :ordered-subtasks(and
            (t1 (t_develop_api ?api))
            (t2 (a_conclude_app_development ?app))
        )
    )
    (:method m_develop_api
        :parameters (?api - system2_api ?wo - webapp_object ?jo - jsondb_object ?ts - task_struct)
        :task (t_develop_api ?api)
        :precondition (and
            (not(api_developed ?api))
        )
        :ordered-subtasks(and
            (t1 (a_create_web_app_object ?wo))
            (t2 (a_create_json_db_object ?jo))
            (t3 (a_develop_task_data_structure ?ts))
            (t4 (t_develop_api_get_tasks ?api))
            (t5 (t_develop_api_create_task ?api))
            (t6 (t_develop_api_decompose_task ?api))
            (t7 (a_conclude_api_development ?api))
        )
    )
    (:method m_develop_api_get_tasks
        :parameters (?api - system2_api ?wo - webapp_object ?jo - jsondb_object)
        :task (t_develop_api_get_tasks ?api)
        :precondition (and
            (not(api_get_tasks_developed ?api))
            (web_app_object_created ?wo)
            (json_db_object_created ?jo)
        )
        :ordered-subtasks(and
            (t1 (a_develop_api_get_tasks ?api ?wo ?jo))
        )
    )
    (:method m_develop_api_create_task
        :parameters (?api - system2_api ?wo - webapp_object ?jo - jsondb_object ?ts - task_struct)
        :task (t_develop_api_create_task ?api)
        :precondition (and
            (not(api_create_task_developed ?api))
            (web_app_object_created ?wo)
            (json_db_object_created ?jo)
            (task_data_struct_developed ?ts)
        )
        :ordered-subtasks(and
            (t1 (a_develop_api_create_task ?api ?wo ?jo ?ts))
        )
    )
    (:method m_develop_api_decompose_task
        :parameters (?api - system2_api ?wo - webapp_object ?jo - jsondb_object ?ts - task_struct)
        :task (t_develop_api_decompose_task ?api)
        :precondition (and
            (not(api_decompose_task_developed ?api))
            (web_app_object_created ?wo)
            (json_db_object_created ?jo)
            (task_data_struct_developed ?ts)
        )
        :ordered-subtasks(and
            (t1 (a_develop_api_decompose_task ?api ?wo ?jo ?ts))
        )
    )

    ; actions
    (:action a_create_web_app_object
        :parameters (?wo - webapp_object)
        :precondition (not(web_app_object_created ?wo))
        :effect (web_app_object_created ?wo)
    )
    (:action a_create_json_db_object
        :parameters (?jo - jsondb_object)
        :precondition (not(json_db_object_created ?jo))
        :effect (json_db_object_created ?jo)
    )
    (:action a_develop_api_get_tasks
        :parameters (?api - system2_api ?wo - webapp_object ?jo - jsondb_object)
        :precondition (and
            (not(api_get_tasks_developed ?api))
            (web_app_object_created ?wo)
            (json_db_object_created ?jo)
        )
        :effect(api_get_tasks_developed ?api)
    )
    (:action a_conclude_api_development
        :parameters (?api - system2_api)
        :precondition ()
        :effect (api_developed ?api)
    )
    (:action a_conclude_app_development
        :parameters (?app - system2_app)
        :precondition ()
        :effect (app_developed ?app)
    )
    (:action a_develop_api_create_task
        :parameters (?api - system2_api ?wo - webapp_object ?jo - jsondb_object ?ts - task_struct)
        :precondition (and
            (not(api_create_task_developed ?api))
            (web_app_object_created ?wo)
            (json_db_object_created ?jo)
            (task_data_struct_developed ?ts)
        )
        :effect(api_create_task_developed ?api)
    )
    (:action a_develop_api_decompose_task
        :parameters (?api - system2_api ?wo - webapp_object ?jo - jsondb_object ?ts - task_struct)
        :precondition (and
            (not(api_decompose_task_developed ?api))
            (web_app_object_created ?wo)
            (json_db_object_created ?jo)
            (task_data_struct_developed ?ts)
        )
        :effect(api_decompose_task_developed ?api)
    )
    (:action a_develop_task_data_structure
        :parameters (?ts - task_struct)
        :precondition (not(task_data_struct_developed ?ts))
        :effect(task_data_struct_developed ?ts)
    )
)